plugins {
	id 'java'
	id 'antlr'
	id 'org.hidetake.ssh' version '1.1.1'
}

repositories { mavenCentral() }

sourceCompatibility = targetCompatibility = 1.7

dependencies {
	antlr 'org.antlr:antlr4:4.5'
	compile files('lib/ev3classes.jar')
}

generateGrammarSource { outputDirectory file("$projectDir/build/generated-src/antlr/main/dkwakkel/jgcode") }

remotes {
	ev3 {
		host = '10.0.1.1'
		user = 'root'
		password = 'does_not_matter_as_long_as_it_is_not_empty'
		knownHosts = allowAnyHosts
	}
}

jar {
	manifest {
		attributes("Class-Path" : "/home/root/lejos/lib/ev3classes.jar /home/root/lejos/libjna/usr/share/java/jna.jar",
		"Main-Class" : "EV3Machine")
	}
	from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
}

task deploy(dependsOn:jar) << {
	ssh.run {
		session(remotes.ev3) {
			println "scp $jar.archivePath.absolutePath /home/lejos/programs"
//			put from: jar.archivePath.absolutePath, into: '/home/lejos/programs'
			execute "jrun -jar /home/lejos/programs/$jar.archiveName"
		}
	}
}

task wrapper(type: Wrapper) { gradleVersion = '2.3' }
